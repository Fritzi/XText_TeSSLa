grammar de.uniluebeck.isp.tessla.TeSSLa with org.eclipse.xtext.common.Terminals

generate teSSLa "http://www.uniluebeck.de/isp/tessla/TeSSLa"

Model:
	spec += statement*;

statement:
	{statement} (def=definition | out=out | in=in | comment=SL_COMMENT)
;
  
@Override 
terminal SL_COMMENT:
    ('--') !('\n'|'\r')* ('\r'? '\n')?
;  
  
definition:
	('def' | 'define') def=ID paramList=paramList? (':' type=type)? ':=' (expression=typedExpression | '{' statements+=statement* expression=expression'}')
;

paramList:
	{paramList} (
	'(' params+=ID (':' types+=type)? (',' params+=ID (':' types+=type)?)* ')'
	| '(' ')'
);

out:
	'out' exp=typedExpression ('as' name=ID)?
;
	
in:
	'in' name=ID ':' type=type
;

typedExpression:
	exp=expression (':' type=type)?
;

expression:
	'if' if=typedExpression 'then' then=typedExpression ('else' else=typedExpression)?
	| val+=value (infix+=infixOperator val+=value)*
;

value:
	{value}
	(op=unaryOperator exp=typedExpression
	| '(' exp=typedExpression ')'
	| '{' statements+=statement* exp=typedExpression '}'
	| name=ID '(' (args+=arg (',' args+=arg)*)? ')'
	| name=ID
	| name=STRING
	| val=INT
	| '()' 
	| 'true' 
	| 'false')
;
	
arg:
	arg=ID '=' exp=typedExpression
	| exp=typedExpression
;

type:
	(ID '<' type (',' type)* '>')
	| ID
;

infixOperator:
	'%'
	| '<<'
	| '>>'
	| '>='
	| '<='
	| '<'
	| '>'
	| '!='
	| '=='
	| '&'
	| '|'
	| '^'
	| '+'
	| '-'
	| '*'
	| '/'
	| '&&'
	| '||'
;

unaryOperator:
	'~'
	| '-'
	| '!'
;
